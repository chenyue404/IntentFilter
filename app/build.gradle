plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 35

    defaultConfig {
        applicationId "com.chenyue404.intentfilter"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 6
        versionName "1.5"
    }

    File certificationConfFile = file("certification.properties")
    Properties certificationConf = new Properties()
    certificationConf.load(new FileInputStream(certificationConfFile))

    signingConfigs {
        my {
            storeFile file(certificationConf['storeFile'])
            storePassword certificationConf['storePassword']
            keyAlias certificationConf['keyAlias']
            keyPassword certificationConf['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.my
        }
        debug {
            signingConfig signingConfigs.my
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            if (variant.buildType.name == 'release') {
                def fileName = "IntentFitler_${defaultConfig.versionCode}_${defaultConfig.versionName}.apk"
                outputFileName = fileName
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    namespace 'com.chenyue404.intentfilter'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'

    compileOnly 'de.robv.android.xposed:api:82'
//    compileOnly 'de.robv.android.xposed:api:82:sources'

    implementation("com.google.code.gson:gson:2.9.0")
}